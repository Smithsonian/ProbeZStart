<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.6">
  <POU Name="MAIN" Id="{0d52fe34-3379-4937-8bfa-de5557a85529}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	state: XY_Stage_State; (* XY Stage State *)
	
	// motor variables
	axisX		: AXIS_REF;
	axisY		: AXIS_REF;
	fbXReset	: MC_Reset;
	fbYReset	: MC_Reset;
	fbXPower	: MC_Power;
	fbYPower	: MC_Power;
	fbXMoveAbs	: MC_MoveAbsolute;
	fbYMoveAbs	: MC_MoveAbsolute;
	bReset		: BOOL := FALSE;
	bXEnable	: BOOL := FALSE;
	bYEnable	: BOOL := FALSE;
	bMove		: BOOL := FALSE;
	fXTargetPos	: LREAL := 0;
	fYTargetPos : LREAL := 0;
	fXTargetVel : LREAL := 1;
	fYTargetVel : LREAL := 1;
	
	// inputs
	{attribute 'OPC.UA.DA' := '1'}
	inputEnableDrive	: BOOL := FALSE;
	{attribute 'OPC.UA.DA' := '1'}
	inputResetDrive		: BOOL := FALSE;
	{attribute 'OPC.UA.DA' := '1'}
	inputXPos	: LREAL := 0;
	{attribute 'OPC.UA.DA' := '1'}
	inputYPos	: LREAL := 0;
	{attribute 'OPC.UA.DA' := '1'}
	inputXVel	: LREAL := 10;
	{attribute 'OPC.UA.DA' := '1'}
	inputYVel	: LREAL := 10;
	
	// outputs
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	XPos		: LREAL; (* X position *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	YPos		: LREAL; (* Y position *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	XVel		: LREAL; (* X velocity *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	YVel		: LREAL; (* Y velocity *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	XLag		: LREAL := 0; (* X position lag *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	YLag		: LREAL := 0; (* Y position lag *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	XLagMax		: LREAL := 0; (* maximum X position lag (abs val) *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	YLagMax		: LREAL := 0; (* maximum Y position lag (abs val) *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	XErrorCode		: UDINT := 0; (* last error code for X *)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	YErrorCode		: UDINT := 0; (* last error code for Y *)
	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// read drive status
axisX.ReadStatus();
axisY.ReadStatus();

// check for errors
IF axisX.Status.Error = TRUE OR axisY.Status.Error = TRUE OR
	fbXPower.Error = TRUE OR fbYPower.Error = TRUE OR
	fbXMoveAbs.Error = TRUE OR fbYMoveAbs.Error = TRUE THEN
	XErrorCode := MAX(axisX.Status.ErrorID, fbXPower.ErrorID, fbXMoveAbs.ErrorID);
	YErrorCode := MAX(axisY.Status.ErrorID, fbYPower.ErrorID, fbYMoveAbs.ErrorID);
	state := XY_Stage_State.error;
END_IF

// process inputs
// these limits taken from Drive Manager
inputXVel := MIN(inputXVel, 93.6);
inputYVel := MIN(inputYVel, 93.6);

// turn off if commanded
IF inputEnableDrive = FALSE THEN
	state := XY_Stage_State.off;
ELSIF inputResetDrive = TRUE THEN // check for reset
	state := XY_Stage_State.reset;
END_IF

// extract outputs
XPos := axisX.NcToPlc.ActPos;
YPos := axisY.NcToPlc.ActPos;
XVel := axisX.NcToPlc.ActVelo;
YVel := axisY.NcToPlc.ActVelo;
XLag := axisX.NcToPlc.PosDiff;
YLag := axisY.NcToPlc.PosDiff;
XLagMax := MAX(ABS(XLag), XLagMax);
YLagMax := MAX(ABS(YLag), YLagMax);

// state machine
CASE state OF
XY_Stage_State.off: // init (power off)
	bReset := FALSE;
	bXEnable := FALSE;
	bYEnable := FALSE;
	bMove := FALSE;
	IF inputEnableDrive = TRUE THEN
		state := XY_Stage_State.reset;
	END_IF
XY_Stage_State.reset: // reset & enable power to axes
	bReset := TRUE;
	bXEnable := TRUE;
	bYEnable := TRUE;
	bMove := FALSE;
	XErrorCode := 0;
	YErrorCode := 0;
	XLagMax := 0;
	YLagMax := 0;
	IF fbXReset.Done = TRUE AND fbYReset.Done = TRUE AND
		fbXPower.Status = TRUE AND fbYPower.Status = TRUE THEN
		bReset := FALSE;
		inputResetDrive := FALSE; // only reset once
		state := XY_Stage_State.ready;
	END_IF
XY_Stage_State.ready: // set up move
	fXTargetPos := inputXPos;
	fYTargetPos := inputYPos;
	fXTargetVel := inputXVel;
	fYTargetVel := inputYVel;
	bMove := TRUE;
	state := XY_Stage_State.moving;
XY_Stage_State.moving: // moving
	IF fbXMoveAbs.Done = TRUE AND fbYMoveAbs.Done = TRUE THEN
		bMove := FALSE;
		state := XY_Stage_State.ready;
	END_IF
XY_Stage_State.error: // error
	RETURN; // lock into this state until reset
END_CASE

// function blocks, execution controlled by their execute bits
fbXReset(Axis := axisX, Execute := bReset);
fbYReset(Axis := axisY, Execute := bReset);

fbXPower(
	Axis := axisX,
	Enable := bXEnable,
	Enable_Positive := bXEnable,
	Enable_Negative := bXEnable,
	Override := 100);
fbYPower(
	Axis := axisY,
	Enable := bYEnable,
	Enable_Positive := bYEnable,
	Enable_Negative := bYEnable,
	Override := 100);
	
fbXMoveAbs(
	Axis:= axisX,
	Execute:= bMove,
	Position:= fXTargetPos,
	Velocity:= fXTargetVel,
	Acceleration:= ,
	Deceleration:= ,
	Jerk:= );
fbYMoveAbs(
	Axis:= axisY,
	Execute:= bMove,
	Position:= fYTargetPos,
	Velocity:= fYTargetVel,
	Acceleration:= ,
	Deceleration:= ,
	Jerk:= );
]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="457" Count="0" />
      <LineId Id="205" Count="1" />
      <LineId Id="458" Count="0" />
      <LineId Id="300" Count="0" />
      <LineId Id="386" Count="0" />
      <LineId Id="403" Count="1" />
      <LineId Id="388" Count="0" />
      <LineId Id="402" Count="0" />
      <LineId Id="405" Count="0" />
      <LineId Id="389" Count="0" />
      <LineId Id="444" Count="4" />
      <LineId Id="471" Count="0" />
      <LineId Id="461" Count="0" />
      <LineId Id="464" Count="0" />
      <LineId Id="469" Count="0" />
      <LineId Id="465" Count="0" />
      <LineId Id="467" Count="0" />
      <LineId Id="466" Count="0" />
      <LineId Id="459" Count="0" />
      <LineId Id="449" Count="7" />
      <LineId Id="440" Count="0" />
      <LineId Id="436" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="143" Count="0" />
      <LineId Id="324" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="308" Count="0" />
      <LineId Id="320" Count="0" />
      <LineId Id="472" Count="2" />
      <LineId Id="146" Count="0" />
      <LineId Id="326" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="216" Count="0" />
      <LineId Id="441" Count="0" />
      <LineId Id="412" Count="1" />
      <LineId Id="475" Count="1" />
      <LineId Id="189" Count="0" />
      <LineId Id="329" Count="0" />
      <LineId Id="406" Count="0" />
      <LineId Id="435" Count="0" />
      <LineId Id="190" Count="1" />
      <LineId Id="154" Count="0" />
      <LineId Id="210" Count="0" />
      <LineId Id="260" Count="0" />
      <LineId Id="311" Count="1" />
      <LineId Id="214" Count="1" />
      <LineId Id="200" Count="0" />
      <LineId Id="193" Count="0" />
      <LineId Id="224" Count="0" />
      <LineId Id="195" Count="1" />
      <LineId Id="384" Count="1" />
      <LineId Id="140" Count="0" />
      <LineId Id="334" Count="0" />
      <LineId Id="331" Count="0" />
      <LineId Id="330" Count="0" />
      <LineId Id="332" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="30" Count="3" />
      <LineId Id="35" Count="0" />
      <LineId Id="229" Count="4" />
      <LineId Id="228" Count="0" />
      <LineId Id="42" Count="0" />
      <LineId Id="44" Count="5" />
      <LineId Id="43" Count="0" />
      <LineId Id="50" Count="0" />
      <LineId Id="243" Count="6" />
      <LineId Id="242" Count="0" />
      <LineId Id="68" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>